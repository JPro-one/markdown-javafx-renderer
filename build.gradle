
buildscript {
  repositories {
    jcenter()
    gradlePluginPortal()

    maven {
      url "https://sandec.jfrog.io/artifactory/repo"
    }
  }

  dependencies {
    classpath "com.sandec.jpro:jpro-plugin-gradle:$JPRO_VERSION"
    classpath 'org.openjfx:javafx-plugin:0.0.13'
  }
}

repositories {
  jcenter()
}


apply plugin: 'java'

group = 'com.sandec'
version = "$MDFX_VERSION"

compileJava {
  sourceCompatibility = 11
  targetCompatibility = 11

  options.compilerArgs << "-Xlint:deprecation"
  options.compilerArgs << "-Xlint:unchecked"
}

apply plugin: 'org.openjfx.javafxplugin'
apply plugin: 'maven-publish'

javafx {
   configuration = "compileOnly"
  version = "$JAVAFX_VERSION"
  modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web' ]
}

dependencies {
  implementation "com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:$FLEXMARK_VERSION"
  implementation "com.vladsch.flexmark:flexmark-ext-gfm-tasklist:$FLEXMARK_VERSION"
  implementation "com.vladsch.flexmark:flexmark-ext-tables:$FLEXMARK_VERSION"
  implementation "com.vladsch.flexmark:flexmark-ext-attributes:$FLEXMARK_VERSION"
}

java.withSourcesJar()

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      groupId project.group
      artifactId "mdfx"
      version project.version
    }
  }
  repositories {
    maven {
      name = "artifactory"
      url = "https://sandec.jfrog.io/artifactory/repo"
      credentials {
        username System.getenv("SANDEC_ARTIFACTORY_USERNAME")
        password System.getenv("SANDEC_ARTIFACTORY_PASSWORD")
      }
    }
  }
}

configure([project(':example')]) {
  apply plugin: 'java'
  apply plugin: 'application'
  apply plugin: 'org.openjfx.javafxplugin'
  apply plugin: 'com.sandec.jpro'

  repositories {
    jcenter()
  }

  javafx {
    version = "$JAVAFX_VERSION"
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web' , 'javafx.swing']
  }

  dependencies {
    implementation rootProject

    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation "fr.brouillard.oss:cssfx:$CSSFX_VERSION"
  }

  mainClassName = 'com.sandec.mdfx.example.ExampleMDFX'
}

