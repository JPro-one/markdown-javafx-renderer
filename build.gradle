
buildscript {
  repositories {
    jcenter()
    gradlePluginPortal()

    maven {
      url "http://sandec.bintray.com/repo"
    }
  }

  dependencies {
    classpath "com.sandec.jpro:jpro-plugin-gradle:$JPRO_VERSION"
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    classpath 'org.openjfx:javafx-plugin:0.0.9'
  }
}

repositories {
  jcenter()
}


apply plugin: 'java'

group = 'com.sandec'
version = "$MDFX_VERSION"

compileJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  options.compilerArgs << "-Xlint:deprecation"
  options.compilerArgs << "-Xlint:unchecked"
}

apply plugin: 'org.openjfx.javafxplugin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

javafx {
  version = "$JAVAFX_VERSION"
  modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web' ]
}

dependencies {
  compile "com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:$FLEXMARK_VERSION"
  compile "com.vladsch.flexmark:flexmark-ext-tables:$FLEXMARK_VERSION"
  compile "com.vladsch.flexmark:flexmark-ext-attributes:$FLEXMARK_VERSION"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      groupId project.group
      artifactId "mdfx"
      version project.version
    }
  }
}

bintray {
  if(project.hasProperty("BINTRAY_USER")) {
    user = BINTRAY_USER
    key  = BINTRAY_KEY
  }
  publications = ['mavenJava']
  publish = true
  override = true
  pkg {
    repo = 'repo'
    name = project.name
    userOrg = 'sandec'
  }
}

configure([project(':example')]) {
  apply plugin: 'java'
  apply plugin: 'application'
  apply plugin: 'org.openjfx.javafxplugin'
  apply plugin: 'com.sandec.jpro'

  repositories {
    jcenter()
  }

  javafx {
    version = "$JAVAFX_VERSION"
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web' ]
  }

  dependencies {
    compile rootProject

    compile group: 'commons-io', name: 'commons-io', version: '2.8.0'
    compile "fr.brouillard.oss:cssfx:$CSSFX_VERSION"
  }

  mainClassName = 'com.sandec.mdfx.ExampleMDFX'
}

